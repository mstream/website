def mavenUser=project.properties["mavenUser"] ?: "";
def mavenPassword=project.properties["mavenPassword"] ?: "";

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
        classpath "net.researchgate:gradle-release:2.3.4"
    }
}

apply plugin: "java"
apply plugin: "application"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "maven-publish"
apply plugin: 'net.researchgate.release'
apply plugin: "idea"

group = "io.mstream.website"

sourceCompatibility = "1.8";
targetCompatibility = "1.8";

mainClassName = "io.mstream.website.Main"

repositories {
    jcenter()
}

configurations {
    provided
    provided.extendsFrom compile
}

sourceSets {
    main {
        compileClasspath += [configurations.provided]
    }
}

dependencies {
    provided "org.projectlombok:lombok:1.16.8"
    testCompile "junit:junit:4.12"
    testCompile "io.vertx:vertx-unit:3.2.1"
    compile "io.vertx:vertx-core:3.2.1"
    compile "io.vertx:vertx-web:3.2.1"
    compile "com.google.inject:guice:4.0"
    compile "ch.qos.logback:logback-classic:1.1.6"
}


idea {
    module {
        scopes.PROVIDED.plus += [configurations.provided]
    }
}

publishing {
    repositories {
        maven {
            url "http://89.40.120.36/nexus/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases' }/"
            credentials {
                username "${mavenUser}"
                password "${mavenPassword}"
            }
        }
    }
    publications {
        shadow(MavenPublication) {
            artifact shadowJar
        }
    }
}

shadowJar {
    classifier = "fat"
    mergeServiceFiles {
        include "META-INF/services/io.vertx.core.spi.VerticleFactory"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.11"
}



